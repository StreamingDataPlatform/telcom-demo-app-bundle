apply plugin: "java"
apply plugin: "application"
apply plugin: "maven-publish"
apply plugin: "com.github.johnrengelman.shadow"

group = "io.pravega"
archivesBaseName = "flinkprocessor"
description = "flinkprocessor"
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = version

mainClassName = "io.pravega.flinkprocessor.PravegaToInfluxDB"


// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// Explicitly define the libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    flinkShadowJar // dependencies which go into the shadowJar

    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'log4j'
}

task wrapper(type: Wrapper){
    gradleVersion = '7.2'
}


dependencies {
    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    implementation "org.apache.flink:flink-java:${flinkVersion}"
    implementation "org.apache.flink:flink-streaming-java:${flinkVersion}"
    implementation "org.apache.flink:flink-table-api-java-bridge:${flinkVersion}"
    implementation "org.apache.flink:flink-table-planner_${flinkScalaVersion}:${flinkVersion}"
    flinkShadowJar "org.apache.flink:flink-connector-jdbc_${flinkScalaVersion}:1.12.3"


    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------
    // Note that we must include the Elasticsearch client early in the classpath to avoid Netty version conflicts.
    flinkShadowJar "org.elasticsearch.client:elasticsearch-rest-high-level-client:6.5.4"
    flinkShadowJar "org.apache.flink:flink-connector-elasticsearch6:${flinkVersion}"
    flinkShadowJar "io.pravega:pravega-connectors-flink-${flinkShortVersion}_${flinkScalaVersion}:${pravegaFlinkConnectorVersion}"
    flinkShadowJar "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    // enable Once copied jar file to libs folder.
    flinkShadowJar files("./libs/flink-connector-influxdb_2.11-1.1-SNAPSHOT.jar")
    flinkShadowJar files("./libs/postgresql-42.2.23.jar")
    flinkShadowJar group: 'org.influxdb', name: 'influxdb-java', version: '2.7'
    flinkShadowJar "org.apache.flink:flink-json:${flinkVersion}"
    flinkShadowJar "org.apache.flink:flink-clients:${flinkVersion}"


    if (includePravegaCredentials.toBoolean()) {
        flinkShadowJar "io.pravega:pravega-keycloak-client:${pravegaCredentialsVersion}"
    }

    // https://mvnrepository.com/artifact/org.json/json
    flinkShadowJar group: 'org.json', name: 'json', version: '20090211'
    // https://mvnrepository.com/artifact/org.apache.httpcomponents/fluent-hc
    flinkShadowJar group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.12'

    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "ch.qos.logback:logback-core:${logbackVersion}"
    implementation "org.scala-lang:scala-compiler:${scalaCompilerVersion}"

    testImplementation "org.apache.flink:flink-test-utils:${flinkVersion}"
    testImplementation "junit:junit:${junitVersion}"
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar

    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar

    javadoc.classpath += configurations.flinkShadowJar
}

run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    zip64 true
    mergeServiceFiles()
    configurations = [project.configurations.flinkShadowJar]
    classifier = ""
}

distributions {
    main {
        distributionBaseName = archivesBaseName
        contents {
            into("lib") {
                from shadowJar
                from(project.configurations.shadow)
            }
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
            url = System.getenv("MAVEN_URL")
        }
    }
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}